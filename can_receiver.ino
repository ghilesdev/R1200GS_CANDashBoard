#include <SPI.h>
#include <ILI9486_SPI.h>
#include <esp_now.h>
#include <WiFi.h>

// Display Pins
#define TFT_CS   10
#define TFT_DC   4
#define TFT_RST  5
#define TFT_MOSI 7
#define TFT_MISO -1
#define TFT_SCLK 6
ILI9486_SPI tft(TFT_CS, TFT_DC, TFT_RST);

// TODO
// add oil warning, lamp fault, hight beam, air temp


// Data structure for ESP-NOW
typedef struct __attribute__((packed)){
  uint16_t rpm;
  uint8_t fuelLevel;
  uint8_t oilTemp;
  uint8_t speed;
  uint8_t gear;
  uint8_t  infoButton; // 4=Off, 5=Short Press, 6=Long Press(>2secs)  
  uint16_t  blinkers; // CF=Off, D7=Left On, E7=Right On, EF=Both On
  int odometer; // (d3,d2,d1)
} canData_t;
canData_t receivedData;

// ESP-NOW callback
void OnDataRecv(const esp_now_recv_info_t *esp_now_info, const uint8_t *incomingData, int len) {
  if (len == sizeof(canData_t)) {
    memcpy(&receivedData, incomingData, len);
  } else {
    Serial.println("impossible to copy received data");
    Serial.println(len);
    Serial.println(sizeof(canData_t));

  }
}

// for drawing
uint8_t segmentHeight = 10;
uint8_t rpmBarStartY = 15;
uint8_t rpmBarStartX = 44;
uint16_t rpmBarLength = 400;
uint16_t rpmBarFillColor = 0xffff;
uint8_t rpmSegmentLength = 5;
uint8_t rpmBarHeight = 30;
uint8_t totalFuelBarHeight = 200; 
uint8_t totalFuelBarWidth = 30; 
uint8_t lastFuelSegmentsNb = 0; 
uint8_t lastRpmSegmentsNb = 0; 
uint8_t lastGear = 0; 
uint8_t lastOilTemp = 0; 
uint8_t lastSpeed = 0; 
uint8_t lastBlinkerStatus = 0; 
uint8_t lastInfoButtonStatus = 0; 
int lastOdometer = 0;
char speed[4]; //buffer to keep speed number on 3 digits

// 'station-essence', 32x32px
const unsigned char gasIcon [] PROGMEM = {
	0x00, 0x00, 0x00, 0x00, 0x1f, 0xff, 0xe0, 0x00, 0x3f, 0xff, 0xf1, 0x80, 0x38, 0x00, 0x3b, 0x80, 
	0x30, 0x00, 0x39, 0xc0, 0x3f, 0xff, 0xf8, 0xe0, 0x3c, 0x00, 0xf8, 0x70, 0x3c, 0x00, 0xf8, 0x38, 
	0x3c, 0x00, 0xf8, 0x7c, 0x3c, 0x00, 0xf8, 0xfc, 0x3c, 0x00, 0xf8, 0xce, 0x3c, 0x00, 0xf8, 0xfe, 
	0x3c, 0x00, 0xf8, 0xfe, 0x3f, 0xff, 0xf8, 0x3e, 0x37, 0xff, 0xb8, 0x0e, 0x30, 0x00, 0x38, 0x0e, 
	0x30, 0x00, 0x3c, 0x0e, 0x30, 0x00, 0x3f, 0x0e, 0x30, 0x00, 0x3f, 0x8e, 0x30, 0x00, 0x3b, 0x8e, 
	0x30, 0x00, 0x3b, 0x8e, 0x30, 0x00, 0x3b, 0x8e, 0x30, 0x00, 0x3b, 0x8e, 0x30, 0x00, 0x39, 0xdc, 
	0x30, 0x00, 0x39, 0xfc, 0x30, 0x00, 0x38, 0xf8, 0x30, 0x00, 0x38, 0x00, 0x30, 0x00, 0x38, 0x00, 
	0x30, 0x00, 0x38, 0x00, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x00, 0x00, 0x00, 0x00
};
const unsigned char oilTempIcon [] PROGMEM = {
	0x00, 0x07, 0xc0, 0x00, 0x00, 0x0f, 0xe0, 0x00, 0x00, 0x0f, 0xe0, 0x00, 0x00, 0x0e, 0xee, 0x00, 
	0x00, 0x0c, 0x6e, 0x00, 0x00, 0x0c, 0x60, 0x00, 0x00, 0x0c, 0x6e, 0x00, 0x00, 0x0c, 0x6e, 0x00, 
	0x00, 0x0c, 0x60, 0x00, 0x00, 0x0c, 0x6e, 0x00, 0x00, 0x0c, 0x6e, 0x00, 0x00, 0x0d, 0x60, 0x00, 
	0x00, 0x0d, 0x6e, 0x00, 0x00, 0x0d, 0x6e, 0x00, 0x00, 0x0d, 0x60, 0x00, 0x00, 0x0d, 0x6e, 0x00, 
	0x00, 0x0d, 0x6e, 0x00, 0x00, 0x0d, 0x60, 0x00, 0x00, 0x0d, 0x6e, 0x00, 0x00, 0x1d, 0x7e, 0x00, 
	0x00, 0x3d, 0x78, 0x00, 0x00, 0x7f, 0xfc, 0x00, 0x00, 0x7f, 0xfc, 0x00, 0x00, 0x7f, 0xfc, 0x00, 
	0x00, 0x7f, 0xfc, 0x00, 0x00, 0x7f, 0xfc, 0x00, 0x00, 0x7f, 0xfc, 0x00, 0x00, 0x7f, 0xfc, 0x00, 
	0x00, 0x7f, 0xfc, 0x00, 0x00, 0x3f, 0xf8, 0x00, 0x00, 0x1f, 0xf0, 0x00, 0x00, 0x07, 0xc0, 0x00
};

const unsigned char rpmIcon [] PROGMEM = {
	0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 
	0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x08, 0x00, 0x00, 0x00, 0x18, 0x00, 0x00, 0x01, 0x98, 0x00, 
	0x00, 0x7d, 0xf8, 0x00, 0x01, 0xfd, 0xff, 0x00, 0x03, 0xff, 0xff, 0x00, 0x03, 0xff, 0xff, 0xc0, 
	0x0f, 0xe0, 0xff, 0xf0, 0x1f, 0x80, 0xff, 0xf0, 0x3e, 0x01, 0xff, 0xf8, 0x7c, 0x01, 0xe3, 0xfc, 
	0x78, 0x03, 0xe3, 0xfc, 0x70, 0x03, 0xe1, 0xfe, 0xe0, 0x07, 0xe0, 0xfe, 0x60, 0x07, 0xc0, 0xf7, 
	0xc0, 0x0f, 0xc0, 0x7f, 0xc0, 0x0f, 0xc0, 0x7f, 0xc0, 0x1f, 0xc0, 0x7f, 0xc0, 0x1d, 0x80, 0x7f, 
	0xc0, 0x1f, 0x80, 0x7f, 0xc0, 0x1f, 0x80, 0x7f, 0xc0, 0x07, 0x00, 0x7f, 0x00, 0x00, 0x00, 0x00, 
	0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};

const unsigned char leftArrowIcon [] PROGMEM = {
	0x00, 0x3f, 0xfc, 0x00, 0x00, 0xff, 0xff, 0x00, 0x03, 0xff, 0xff, 0xc0, 0x07, 0xff, 0xff, 0xe0, 
	0x0f, 0xff, 0xff, 0xf0, 0x1f, 0xff, 0xff, 0xf8, 0x3f, 0xff, 0xff, 0xfc, 0x3f, 0xff, 0xff, 0xfc, 
	0x7f, 0xff, 0xff, 0xfe, 0x7f, 0xff, 0xff, 0xfe, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfc, 0xff, 0xff, 
	0xff, 0xf8, 0xff, 0xff, 0xff, 0xe0, 0xff, 0xff, 0xff, 0x80, 0x00, 0x7f, 0xff, 0x00, 0x00, 0x7f, 
	0xff, 0x00, 0x00, 0x7f, 0xff, 0x80, 0x00, 0x7f, 0xff, 0xe0, 0xff, 0xff, 0xff, 0xf8, 0xff, 0xff, 
	0xff, 0xfc, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x7f, 0xff, 0xff, 0xfe, 0x7f, 0xff, 0xff, 0xfe, 
	0x3f, 0xff, 0xff, 0xfc, 0x3f, 0xff, 0xff, 0xfc, 0x1f, 0xff, 0xff, 0xf8, 0x0f, 0xff, 0xff, 0xf0, 
	0x07, 0xff, 0xff, 0xe0, 0x03, 0xff, 0xff, 0xc0, 0x00, 0xff, 0xff, 0x00, 0x00, 0x3f, 0xfc, 0x00
};

const unsigned char rightArrowIcon [] PROGMEM = {
	0x00, 0x1f, 0xf8, 0x00, 0x00, 0x7f, 0xfe, 0x00, 0x01, 0xff, 0xff, 0x80, 0x03, 0xff, 0xff, 0xc0, 
	0x07, 0xff, 0xff, 0xe0, 0x0f, 0xff, 0xff, 0xf0, 0x1f, 0xff, 0xff, 0xf8, 0x3f, 0xff, 0xff, 0xfc, 
	0x3f, 0xff, 0xff, 0xfc, 0x7f, 0xff, 0xff, 0xfe, 0x7f, 0xff, 0x7f, 0xfe, 0xff, 0xff, 0x1f, 0xff, 
	0xff, 0xff, 0x0f, 0xff, 0xff, 0xff, 0x03, 0xff, 0xfe, 0x00, 0x01, 0xff, 0xfc, 0x00, 0x00, 0x7f, 
	0xfc, 0x00, 0x00, 0x7f, 0xfe, 0x00, 0x01, 0xff, 0xff, 0xff, 0x03, 0xff, 0xff, 0xff, 0x0f, 0xff, 
	0xff, 0xff, 0x1f, 0xff, 0x7f, 0xff, 0x7f, 0xfe, 0x7f, 0xff, 0xff, 0xfe, 0x3f, 0xff, 0xff, 0xfc, 
	0x3f, 0xff, 0xff, 0xfc, 0x1f, 0xff, 0xff, 0xf8, 0x0f, 0xff, 0xff, 0xf0, 0x07, 0xff, 0xff, 0xe0, 
	0x03, 0xff, 0xff, 0xc0, 0x01, 0xff, 0xff, 0x80, 0x00, 0x7f, 0xfe, 0x00, 0x00, 0x1f, 0xf8, 0x00
};

void drawXBM(int16_t x, int16_t y, const unsigned char *bitmap, int16_t w, int16_t h, uint16_t color, bool hflip) {
  for (int16_t j = 0; j < h; j++) {
    for (int16_t i = 0; i < w; i++) {
      uint8_t byteIndex;
      uint8_t bitIndex = i % 8;;
      int16_t xOffset;
      if (hflip) {
        byteIndex = j * (w / 8) + (w / 8 - 1 - (i / 8));
        xOffset = (w - 1 - i);
      } else {
        byteIndex = j * (w / 8) + i / 8;
        xOffset = i;
      }
      if (pgm_read_byte(&bitmap[byteIndex]) & (1 << (7 - (bitIndex)))) {
        tft.drawPixel(x + xOffset, y + j, color);
      }
    }
  }
}

void setup() {
  Serial.begin(115200);
  SPI.begin(TFT_SCLK, TFT_MISO, TFT_MOSI, TFT_CS);
  tft.init();
  tft.setRotation(1);
  tft.fillScreen(0x0000);
  tft.setTextColor(0xFFFF, 0x0000);
  tft.setTextSize(2);

  // Initialize ESP-NOW
  WiFi.mode(WIFI_STA);
  if (esp_now_init() != ESP_OK) {
    Serial.println("ESP-NOW init failed");
    return;
  }
  Serial.println(WiFi.macAddress());
  esp_now_register_recv_cb(OnDataRecv);

  // draw gauges
  tft.drawRect(446, 110, totalFuelBarWidth, totalFuelBarHeight+1, 0xffff); //fuel
  
  // rpm ticks
  for (uint8_t i=0; i <=8; i++) {
    tft.setCursor(rpmBarStartX + (i*50), 0);
    tft.setTextColor(0x0fff);
    tft.println(i);
  }
  tft.setTextColor(0xffff);

  tft.drawRect(rpmBarStartX, rpmBarStartY, rpmBarLength+1, rpmBarHeight, 0xffff); //rpm
  tft.drawRect(395, 264, 50, 50, 0x0fff); // gear indcator
  drawXBM(446, 78, gasIcon, 32, 32, 0xf00f, false); //gas icon
  drawXBM(10, 278, oilTempIcon, 32, 32, 0xf00f, false); // oil temp icon
  drawXBM(10, 10, rpmIcon, 32, 32, 0xf00f, false); // rpm icon

  // initialize fields
    tft.fillRect(45, 278, 100, 32, 0x0000); // Clear oil temp area
    tft.setCursor(45, 278);
    tft.setTextSize(4);
    tft.print("-- C");
    tft.setTextSize(2);


    tft.fillRect(100, 100, 150, 60, 0x0000); // Clear speed area
    tft.setCursor(100, 100);
    tft.setTextSize(8);
    tft.print("---");
    tft.setTextSize(3);
    tft.print(" KM/h");
    tft.setTextSize(2); //always reset to default text size

    tft.fillRect(100, 180, 150, 30, 0x0000); 
    tft.setCursor(100, 180);
    tft.setTextSize(3); 
    tft.print("ODO: ------ KM");
    tft.setTextSize(2); //always reset to default text size

    tft.fillRect(396, 265, 48, 48, 0x0000); // Clear gear indicator area
    tft.setCursor(406, 275);
    tft.setTextSize(5);
    tft.print("-");
    tft.setTextSize(2);
}

void loop() {

  if (receivedData.infoButton != lastInfoButtonStatus) {
    tft.fillRect(10, 50, 200, 20, 0x0000); // Clear info value area
    tft.setCursor(10, 50);
    tft.print("Info button: ");
    tft.print(receivedData.infoButton);
    lastInfoButtonStatus = receivedData.infoButton;
  }

  uint8_t blinkerStatus = receivedData.blinkers;
  if (blinkerStatus != lastBlinkerStatus){
    if (blinkerStatus == 0) {
      tft.fillRect(10, 230, 32, 32, 0x0000);
      tft.fillRect(400, 230, 32, 32, 0x0000);
    } else if (blinkerStatus == 1) {
      drawXBM(10, 230, leftArrowIcon, 32, 32, 0x0f0f, false); 
      tft.fillRect(400, 230, 32, 32, 0x0000);
    } else if (blinkerStatus == 2) {
      tft.fillRect(10, 230, 32, 32, 0x0000);
      drawXBM(400, 230, rightArrowIcon, 32, 32, 0x0f0f, false); 
    } else if (blinkerStatus == 3) {
      drawXBM(10, 230, leftArrowIcon, 32, 32, 0x0f0f, false); 
      drawXBM(400, 230, rightArrowIcon, 32, 32, 0x0f0f, false); 

    } else {
      Serial.println("received unexpected data for blinkers:");
      Serial.println(blinkerStatus);
    }
    lastBlinkerStatus = receivedData.blinkers;
  }

  // oil data
  if (receivedData.oilTemp != lastOilTemp){
    tft.fillRect(45, 278, 100, 32, 0x0000); // Clear oil temp area
    tft.setCursor(45, 278);
    tft.setTextSize(4);
    tft.print(receivedData.oilTemp);
    tft.print(" C");
    tft.setTextSize(2); //always reset to default text size
    lastOilTemp = receivedData.oilTemp;
  }

  // speed data
  if (receivedData.speed != lastSpeed){
    snprintf(speed, sizeof(speed), "%03d", receivedData.speed);
    tft.fillRect(100, 100, 150, 60, 0x0000); // Clear speed area
    tft.setCursor(100, 100);
    tft.setTextSize(8);
    tft.print(speed);
    tft.setTextSize(3); //always reset to default text size
    tft.print(" KM/h");
    tft.setTextSize(2); //always reset to default text size
    lastSpeed = receivedData.speed;
  }

  // odometer
  if (receivedData.odometer != lastOdometer) {
    tft.fillRect(100, 180, 150, 30, 0x0000); 
    tft.setCursor(100, 180);
    tft.setTextSize(3); 
    tft.print("ODO: ");
    tft.print(receivedData.odometer);
    tft.print(" KM");
    tft.setTextSize(2); //always reset to default text size
    lastOdometer = receivedData.odometer;
  }



  // fuel level:
  uint8_t segmentsNb = (uint8_t) map(receivedData.fuelLevel, 0, 100, 0, 20);

  if (lastFuelSegmentsNb != segmentsNb){
    if (segmentsNb > lastFuelSegmentsNb){ // draw only added segments
      tft.fillRect(447, 310-(segmentHeight*segmentsNb), totalFuelBarWidth-2, (segmentsNb-lastFuelSegmentsNb)*segmentHeight, 0xffff);
    } else if (segmentsNb < lastFuelSegmentsNb) { // less fuel, erase corresponding segemnts
      tft.fillRect(447, 310-(segmentHeight*lastFuelSegmentsNb), totalFuelBarWidth-2, (lastFuelSegmentsNb-segmentsNb)*segmentHeight, 0x0000);
    }
    lastFuelSegmentsNb = segmentsNb;
  }

  // rpm gauge:
  uint8_t rpmSegmentsNb = (uint8_t) map(receivedData.rpm, 0, 8000, 0, 80);

  if (lastRpmSegmentsNb != rpmSegmentsNb){
    if (rpmSegmentsNb > 60) {
      rpmBarFillColor = 0xf00f;
    } else {
      rpmBarFillColor = 0xffff;
    }
    if (rpmSegmentsNb > lastRpmSegmentsNb){ // draw only added segments
      tft.fillRect(rpmBarStartX + 1 , rpmBarStartY + 1, rpmSegmentsNb*rpmSegmentLength, rpmBarHeight-2, rpmBarFillColor);
    } else if (rpmSegmentsNb < lastRpmSegmentsNb) { // decreasing rpm, erase corresponding segemnts
      tft.fillRect(rpmBarStartX + 1  + rpmSegmentsNb*rpmSegmentLength, rpmBarStartY + 1, lastRpmSegmentsNb*rpmSegmentLength, rpmBarHeight-2, 0x0000);
      tft.fillRect(rpmBarStartX + 1 , rpmBarStartY + 1, rpmSegmentsNb*rpmSegmentLength, rpmBarHeight-2, rpmBarFillColor);
    }
    lastRpmSegmentsNb = rpmSegmentsNb;
  }

  // Gear indicator
  uint8_t gearLabel = receivedData.gear;
  if (gearLabel != lastGear) {
    tft.fillRect(396, 265, 48, 48, 0x0000); // Clear gear indicator area
    tft.setCursor(406, 275);
    tft.setTextSize(5);
    if (gearLabel == 0){
      tft.setTextColor(0x0f0f);
      tft.print("N");
      tft.setTextColor(0xffff);
    } else {
      tft.print(gearLabel);
    }
    tft.setTextSize(2);
    lastGear = gearLabel;
  }


  delay(50); // ~10 FPS refresh rate
}
